apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion versions.compile_sdk

    defaultConfig {
        minSdkVersion versions.min_sdk
        targetSdkVersion versions.target_sdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

}

dokkaHtml {
    outputDirectory.set(new File(buildDir, 'dokka'))
}

dependencies {
    implementation(
            deps.kotlin_stdlib,
            deps.appcompat,
            deps.core_ktx
    )
    testImplementation(
            deps.junit
    )
}

tasks.create("updateReadmeVersion") {
    def readmeFile = new File(rootDir, "README.md")
    def readme = readmeFile.text
    def oldDependency = readme.find("implementation '$GROUP:$POM_ARTIFACT_ID:(.+)'")
    def newDependency = "implementation '$GROUP:$POM_ARTIFACT_ID:$VERSION_NAME'"
    readmeFile.text = readme.replace(oldDependency, newDependency)
}

apply from: rootProject.file('gradle/maven-android-prepare.gradle')
apply from: rootProject.file('gradle/maven-publish.gradle')
